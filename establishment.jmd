---
title : GrowthMaps.jl example
author : Rafael Schouten, James Maino
---

# Growth rate calculation

In this example we will calculate the expected population growth rates of our species.

## Load some required packages

These packages take care of loading and plotting data, and handling sci units and dates.

```julia; results=hidden
using GrowthMaps, Plots, Unitful, UnitfulRecipes, Dates, Setfield, Statistics
using GeoData
using ArchGDAL, NCDatasets
using Unitful: °C, K, cal, mol
basedir = ""
```


## Define model components

First we'll define the growth model using `SchoolfieldIntrinsicGrowth`, based on
Schoolfield (1981).

When defining model components, the first parameter is a `:symbol` for the
required raster layer in the source data.

```julia; results=hidden
p = 3.626804e-01
ΔH_A = 3.586625e4cal/mol
ΔH_H = 1.431237e5cal/mol
Thalf_H = 2.988454e2K
ΔH_L = -1.108988e5cal/mol
Thalf_L = 2.459949e2K
T_ref = K(25.0°C)
growthmodel = SchoolfieldIntrinsicGrowth(p, ΔH_A, ΔH_L, Thalf_L, ΔH_H, Thalf_H, T_ref)
growth = Layer(:surface_temp, growthmodel)
```

Now plot the growth rate curve:

```julia; results=hidden
temprange = (270.0:0.1:320.0)u"K"
p = plot(x -> GrowthMaps.rate(growth, x), temprange; label="growth rate")
```

We can try tweaking the fitting the model manually in a user interface.
Model components are immutable (for performance reasons), so we wrap the model
in a mutable wraper so we can use the results.
We parametrise the model over the same temperature range that we are plotting,
using the :surface_temp key that the model requires:

```julia; results=hidden
wrapper = ModelWrapper(growth)
tempdata=(surface_temp=temprange,)
manualfit!(wrapper, tempdata)
```


If you are happy with the result, you we can update extract the manual fit
to use to generate our growth rate maps:

```julia; results=hidden
growth = wrapper.model
```


Note that `manualfit!` will also work for a tuple of model components
that use the same source data, like `(growth, heatstress, coldstress)`.


## Load spatial data

Using the monthly aggregated SMAP dataset

```julia; results=hidden
using HDF5
smap_folder = "/home/raf/Data/drosophila/climate/SMAP_raw"
series = SMAPseries(smap_folder)
ti = dims(series, Ti)
startdate = first(ti)
enddate = last(ti) + Hour(3)
```


Otherwise get the series from tif files:

```julia; results=hidden
# unzippedfolder = "/home/raf/Work/cesar/Data/Raster/SMAP_1_and_16_for_2016/SMAP_based_layers"
# filenames = readdir(unzippedfolder);
# wilting_filenames = filter(fn -> occursin(r"land_fraction_wilting", fn), filenames)
# surface_temp_filenames = filter(fn -> occursin(r"surface_temp", fn), filenames)
# wilting_paths = joinpath.(Ref(unzippedfolder), wilting_filenames)
# surface_temp_paths = joinpath.(Ref(unzippedfolder), surface_temp_filenames)
```

Get the dates covered in the data from the `surface_temp` files list, using regex:

```julia; results=hidden
#df = DateFormat("yyyymmddTHHMMSS");
#dates = DateTime.(replace.(surface_temp_paths, Ref(r".*_(\d+T\d+).tif" => s"\1")), Ref(df));
```

We know the "land_fraction_wilting" files are for the same dates.

Now we have the files and date seies, we can put together
a series of GeoData.jl stacks to load from disk while generating growth maps
The beauty of this approach is that we can use a lot of different source file types and
folder configurations without converting them or running out of RAM.

```julia; results=hidden
# function makestack(i)
#     GDALstack((land_fraction_wilting=wilting_paths[i],
#     surface_temp=surface_temp_paths[i]); window=(Band(1),))
# end
# stacks = [makestack(i) for i in 1:length(surface_temp_paths)]
# startdate = dates[1]
# enddate = dates[end] + Hour(3)
# timedim = Ti(dates; grid=Sampled(Ordered(), Regular(Hour(3), Intervals(Start()))))
# series = GeoSeries(stacks, (timedim,));
```


We can plot a layer from a file at some date in the series:

```julia; results=hidden
series[Ti(Near(DateTime(2016, 2, 1, 8)))][:surface_temp] |> plot
```

First we'll run this basic growth model:

```julia; results=hidden
period = Month(1)
nperiods = 12
subperiod = Day(1)
growthrates = mapgrowth(growth, series;
                   period=period,
                   nperiods=nperiods,
                   subperiod=subperiod,
                   startdate=startdate
                  )
growthrates[Ti(1:3:12)] |> plot
```

Now define some stressors:

```julia; results=hidden
# lowerSM  =  0 # no data
# upperSM  =  0.8 # no data
# upperSMm =  10# no data
# lowerCT  = -10  # Enriquez2017
# lowerCTm = log(1.23) # Stephens2015
# lowerCTm = log(1.00) # testing no cold mortality
# upperCT  = 30 # Kimura2004
# upperCTm = log(1.15)

# Critical minimum temperature, °C -10
# Mortality rate per cold stress, °C/d 0.21
# Critical maximum temperature, °C 35
# Mortality rate per heat stress, °C/d 0.365
# Critical wilting fraction, - 0.50
# Mortality rate per desiccation stress, 1/d 4.6

coldthresh = -10.0°C |> K
coldmort = log(1.23) * K^-1
coldstress = Layer(:surface_temp, ColdStress(coldthresh, coldmort))

heatthresh = 30.0°C |> K
heatmort = log(1.15) * K^-1
heatstress = Layer(:surface_temp, HeatStress(heatthresh, heatmort))

wiltthresh = 0.5
wiltmort = 4.6 # log(1.1) ??
wiltstress = Layer(:land_fraction_wilting, WiltStress(wiltthresh, wiltmort));
```


To build a more complex model, we can chain components together in a tuple,
and run them:

```julia; results=hidden
model = growth, heatstress, coldstress, wiltstress;
growthrates = mapgrowth(model, series;
                   period=period,
                   nperiods=nperiods,
                   subperiod=subperiod,
                   startdate=startdate
                  )
growthrates[Ti(1:3:12)] |> plot
```

## Compare with observation data

To compare out simulation with observations data, we'll load them
from a CSV file:

```julia; results=hiddeats
using CSV
csvfilename = "data/Oersted_occurrence.csv"
obs = CSV.File(csvfilename)
occurrence = collect(zip(obs.Longitude, obs.Latitude))
```

And scatter them on the growthrates map:
```julia; results=hiddeats
p = growthrates[Ti(1)] |> plot
scatter!(p, occurrence; markersize=4.0, markercolor=:green, markershape=:circle, label="obs")
```

# Parametrising models using interactive maps

If you need to adjust the model based on the distribution, this
can be done live in the interface, as with the manual fit.

Parametrising maps on large datasets is processor intensive, which inhibits
interactive fedback. To reduce processing, we can aggregate the spatial data to
a more manageable size.

You can experiment with the `agg` size to compromise between quality and render time. Large values
will look pixelated but will run fast.

```julia; results=hidden
agg = 10
aggseries = aggregate(Center(), series, (Lon(agg), Lat(agg)), (:land_fraction_wilting, :surface_temp));
p = first(aggseries[1]) |> plot
scatter!(p, occurrence; markersize=4.0, markercolor=:green, markershape=:circle, label="obs")
```

As the combination of model is additive, we can prebuild parts of the model
we don't want to fit manually, which simplifies the interfaces and helps performance.
Seeing we allready fit the growth response to empiracle data, lets just fit
the stress responses to the map:

```julia; results=hidden
startdate=DateTime(2016, 1)
period = Month(12)
nperiods = 1
subperiod = Day(1)
modelkwargs = (; period=period, nperiods=nperiods, subperiod=subperiod, startdate=startdate)
precomputed = mapgrowth(growth, aggseries; modelkwargs...)
wrapper = ModelWrapper(wiltstress, coldstress, heatstress)
mapfit!(wrapper, aggseries, modelkwargs;
        occurrence=occurrence,
        precomputed=precomputed,
        throttle=0.4,
        markershape=:circle,
        markercolor=:green,
        markeropacity=0.6
)
```

And get the updated model components from the wrapper:

```julia; results=hidden
wiltstress, coldstress, heatstress = wrapper.model
```

Now we will put together decent population growth maps
using higher resolutions data, and a monthly timestep:

```julia; results=hidden
startdate = DateTime(2016)
period = Month(1)
nperiods = 12
subperiod = Day(1)

model = growth, wiltstress, coldstress, heatstress
growthrates = mapgrowth(model, series;
                   period=period,
                   nperiods=nperiods,
                   subperiod=subperiod,
                   startdate=startdate
                  );
plot(growthrates[Ti(1:3:12)]; clims=(0.0, 0.15), axis=false)
```

This is ready to use for projecting growthrates, or in a dispersal
simulation using Dispersal.jl. Lets save it as a NetCDF file:

```julia; results=hidden
write("data/growthrates.ncd", NCDarray, growthrates)
```

![Growth Rates](../assets/growthrates.png)

We can load the data again with:

```julia; results=hidden
growthrates = NCDarray("data/growthrates.ncd")

plot(growthrates[Ti(1:3:12)]; clims=(0.0, 0.11), axis=false, legend=false, title="")
savefig("output/population_growth_quarters.png")

meangrowth = @set mean(growthrates, dims=Ti).name = "Population growth rate potential (1/d)"
p = plot(meangrowth;
    clims=(0.0, 0.15),
    title="Fall Armyworm mean growth rate"
)
scatter!(p, occurrence;
    markersize=0.3,
    markeralpha=0.7,
    markerstrokecolor=:lightgreen,
    markercolor=:green,
    markershape=:circle,
    label="observations"
)
savefig("output/population_growth_mean.png")
```

Now, if you need to use the model for a serious application, run it on the real
SMAP dataset. GeoData.jl has a `SMAPseries()` constructor that will automate the
whole process of loading real SMAP HDF5 files.
